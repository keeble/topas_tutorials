'=== FILE HEADER ===
r_wp 0 r_exp 0 r_p 0 r_wp_dash 0 r_p_dash 0 r_exp_dash 0 weighted_Durbin_Watson 0 gof 0
iters 100000 'Maximum number of iterations of refinement
chi2_convergence_criteria 0.001 'Stop criteria for refinement
do_errors 'Reports errors for each refined value
'continue_after_convergence 'Continues refinement even after reaching the stop criteria

'=== PDF FILE ===
xdd Disordered_LNMO.xye

'=== FILE PREPARATION ===
	pdf_data
	neutron_data
				
	start_X 1 'Removes lower r values from future calculations		
	finish_X 50 'Removes higher r values from future calculations
	
	dQ_damping( ,0.03)

'=== PHASE INFORMATION ===
	str
		phase_name "disordered"
		a aLP 8.1667   
		b =Get(a);   
		c =Get(a);   
		al 90.      
		be 90.      
		ga 90.      
		volume 544.68   
		space_group "Fd-3mZ"
		site Li1    x 0.125         y 0.125         z 0.125         occ 7Li 1.     beq_spherical(beqloLi,0.1,beqhiLi,0.2,beqradius,20)   
		site Ni1    x 0.5           y 0.5           z 0.5           occ  Ni 0.25   beq_spherical(beqloM ,0.1,beqhiM ,0.2,beqradius,20)      
		site Mn1    x 0.5           y 0.5           z 0.5           occ  Mn 0.75   beq_spherical(beqloM ,0.1,beqhiM ,0.2,beqradius,20)      
		site O1     x @ 0.2634      y =Get(x);      z =Get(x);      occ  O  1.     beq_spherical(beqloO ,0.1,beqhiO ,0.2,beqradius,20)
		scale prmscale 1	
		longrange_sphere(stlrradius,10)
			
			
'=== PHASE INFORMATION ===
	str
		phase_name "ordered"
		a =aLP;
		b =Get(a);    
		c =Get(a);    
		al 90.      
		be 90.      
		ga 90.      
		volume  545.339
		space_group "P4332"
		site Li1    x @  0.00900  y =Get(x);      z =Get(x);      	occ 7Li   1.             beq_spherical(beqloLi,0.1,beqhiLi,0.2,beqradius,20)
		site Ni1    x 0.625       y 0.625         z 0.625         	occ  Ni   =1-occMn;      beq_spherical(beqloM ,0.1,beqhiM ,0.2,beqradius,20)
		site Mn1    x 0.625       y 0.625         z 0.625         	occ  Mn   occMn 0.075    beq_spherical(beqloM ,0.1,beqhiM ,0.2,beqradius,20)
		site Mn2    x 0.125       y yMn2 0.38000  z =-Get(y)+0.25;  occ  Mn   =1-occMn/3;    beq_spherical(beqloM ,0.1,beqhiM ,0.2,beqradius,20)
		site Ni2    x 0.125       y =yMn2;  		z =-Get(y)+0.25;  occ  Ni   =occMn/3;      beq_spherical(beqloM ,0.1,beqhiM ,0.2,beqradius,20)
		site O1     x @  0.38450  y =Get(x);      z =Get(x);        occ  O    1.             beq_spherical(beqloO ,0.1,beqhiO ,0.2,beqradius,20)
		site O2     x @  0.14700  y @ -0.14200    z @  0.12590     	occ  O    1.             beq_spherical(beqloO ,0.1,beqhiO ,0.2,beqradius,20)
		scale =prmscale;
		local_sphere( ,stlrradius)
			
