'=== FILE HEADER ===
r_wp 0 r_exp 0 r_p 0 r_wp_dash 0 r_p_dash 0 r_exp_dash 0 weighted_Durbin_Watson 0 gof 0
iters 100000 'Maximum number of iterations of refinement
chi2_convergence_criteria 0.001 'Stop criteria for refinement
'do_errors 'Reports errors for each refined value
'continue_after_convergence 'Continues refinement even after reaching the stop criteria

prm !dQ    0.05275
prm !alpha 0.00449
prm !lor   0.37618

#define Bragg
#define PDF_Qmax38
#define PDF_Qmax25
#define PDF_Qmax20
#define PDF_Qmax38_Lorch

#ifdef PDF_Qmax38
'=== PDF FILE ===
xdd XPDF_Si_Qmax38_dofr.xy

'=== FILE PREPARATION ===
	pdf_data
	weighting 1
	start_X 1
	dQ_lor_damping( ,dQ, ,lor)
	convolute_alpha( ,alpha)

'=== PHASE INFORMATION ===
	str
#endif

#ifdef PDF_Qmax25
'=== PDF FILE ===
xdd XPDF_Si_Qmax25_dofr.xy

'=== FILE PREPARATION ===
	pdf_data
	weighting 1
	start_X 1
	dQ_lor_damping( ,dQ, ,lor)
	convolute_alpha( ,alpha)
	convolute_Qmax_Sinc( ,25)

'=== PHASE INFORMATION ===
	str
#endif

#ifdef PDF_Qmax20
'=== PDF FILE ===
xdd XPDF_Si_Qmax20_dofr.xy

'=== FILE PREPARATION ===
	pdf_data
	weighting 1
	start_X 1
	dQ_lor_damping( ,dQ, ,lor)
	convolute_alpha( ,alpha)
	convolute_Qmax_Sinc( ,20)	

'=== PHASE INFORMATION ===
	str
#endif
	

#ifdef PDF_Qmax38_Lorch
'=== PDF FILE ===
xdd XPDF_Si_Qmax38_withSoperLorch_dofr.xy

'=== FILE PREPARATION ===
	pdf_data
	weighting 1
	start_X 1
	dQ_lor_damping( ,dQ, ,lor)
	convolute_alpha( ,alpha)	
	convolute_SoperLorch(!d_zero,0.1)
		

'=== PHASE INFORMATION ===
	str
#endif


#ifdef Bragg
'=== POWDER DIFFRACTION FILE ===
xdd XPDF_Si_Bragg.xy

'=== FILE PREPARATION ===
x_calculation_step = Yobs_dx_at(Xo); 'Sets the calculation step size for Rietveld refinement.
finish_X 25 'Removes higher 2th values from future calculations

'=== BACKGROUND FUNCTION ===
bkg @ 0 0 0 0 0 0 
      0 0 0 0 0 0 
bkg_file("XPDF_Capillary_Bragg.xy",@,1)

'=== RADIATION SOURCE - Synchrotron X-ray source with area detector (polarisation corrected data) ===
LP_Factor( ,0) 'Lorentz-Polarisation factor
lam ymin_on_ymax 0.0001 la 1.0 lo =12.3984191/76.69; : 0 lh 0.1 'Radiation profile
			Rs  200 'Sample-to-detector distance (mm)
			
'========================================================================
'7. RIETVELD REFINEMENT PHASE - Phase information for Reitveld Refinement
'========================================================================
	str
		scale @ 0.001
		pkshape_dQ_alpha( ,dQ, ,alpha, ,lor)
#endif
		
for xdds {
	for strs {
		a 5.43119  
		b 5.43119  
		c 5.43119  
		al 90       
		be 90       
		ga 90       
		space_group "Fd-3m"
		site Si1    x 0.000000      y 0.000000      z 0.000000      occ Si   1.0        	beq_rcut_spherical(!rcut,2.7,beqcut,0.1,beqlo,0.1,beqhi,2.0,beqradius,20)
	}
}
